[private]
default:
    @just --list

# Internal alias to execute the `atomic` command
[private]
atomic *args:
    #!/bin/bash
    just -f /etc/Justfile {{ args }}

# Bootstrap the current installation by installing brew & requested flatpaks.
[group('Bootstrap')]
bootstrap:
    @just install-taps
    @just copy-default-configs
    @just enable-spotlight-wallpapers

# Install brew
[group('Brew')]
[private]
install-brew:
    /bin/bash -c "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/HEAD/install.sh)"

[group('Brew')]
[private]
install-taps:
    /home/linuxbrew/.linuxbrew/bin/brew install gdb eza valgrind yazi zellij hyperfine luajit luarocks starship 

# Install brew packages `atomic brew ..`
[group('Packages')]
brew *pkgs:
    /home/linuxbrew/.linuxbrew/bin/brew install {{ pkgs }}

# Install flatpak packages
[group('Packages')]
flatpak *pkgs:
    #!/usr/bin/bash
    function flatpakif() {
        set -eoux pipefail
        if [[ -z "$(which flatpak)" ]]; then
            echo -e "flatpak is not installed";
        else
            flatpak $@
        fi
    }
    flatpakif install flathub --assumeyes --user {{ pkgs }}

[group('Bootc')]
target variant="base" version="42":
    sudo bootc switch --apply quay.io/ddimitrov/hyprland-atomic:{{ version }}-{{ variant }}
    sudo rpm-ostree rebase ostree-remote-registry:quay:quay.io/ddimitrov/hyprland-atomic:{{version}}-{{variant}}

[group('Bootstrap')]
copy-default-configs:
    #!/usr/bin/bash
    if [[ ! -f ~/.config/hypr/hyprland.conf ]]; then
        cp /etc/extra/user_configs/hypr ~/.config/
    else
        echo "Hyprland configurations exists in home, skipping"
    fi
    if [[ ! -f ~/.config/ghostty/config ]]; then
        mkdir -p ~/.config/ghostty
        cp /etc/extra/user_configs/ghostty/config ~/.config/ghostty/config
    else
        echo "Ghostty configuration exists in home, skipping"
    fi
    if [[ ! -f ~/.config/ripgrep/rg.conf ]]; then
        mkdir -p ~/.config/ripgrep
        cp /etc/extra/user_configs/ripgrep/rg.conf ~/.config/ripgrep/rg.conf
    else
        echo "Ghostty configuration exists in home, skipping"
    fi
    if [[ ! -f ~/.config/rofi/config.rasi ]]; then
        mkdir -p ~/.config/rofi
        echo "@theme \"/usr/share/rofi/themes/hyprland-atomic.rasi\"" > ~/.config/rofi/config.rasi
    else
        echo "Rofi configuration exists in home, skipping"
    fi
    if [[ ! -f ~/.config/nvim/init.lua ]]; then
        mkdir -p ~/.config/nvim
        cp -r /etc/extra/user_configs/nvim ~/.config/
    else
        echo "Nvim configuration exists in home, skipping"
    fi
    if [[ ! -f ~/.config/waybar/config.jsonc ]]; then
        mkdir -p ~/.config/waybar
        cp -r /etc/extra/user_configs/waybar ~/.config/
    else
        echo "Nvim configuration exists in home, skipping"
    fi

# Switch from the ISP-provided DNS servers to those provided by Cloudflare
[group('Bootstrap')]
use-cloudflare-dns:
    resolvectl
    @read -p "Add to interface position: " _pos && resolvectl dns $_pos 1.1.1.1 1.0.0.1

# Installs EasyEffects preset from https://github.com/JackHack96/EasyEffects-Presets
[group('Bootstrap')]
install-easyeffects-presets:
    bash -c "$(curl -fsSL https://raw.githubusercontent.com/JackHack96/PulseEffects-Presets/master/install.sh)"

# Enable Spotlight wallpaper rotation scripts that download the images, convert them and then apply them randomly at intervals
[group('Bootstrap')]
enable-spotlight-wallpapers:
    systemctl enable --now --user download-wallpapers.timer
    systemctl enable --now --user rotate-wallpaper.timer
    /usr/local/bin/download-spotlight-wallpapers
    /usr/local/bin/rotate-spotlight-wallpaper

# Update packages & system
[group('Maintenance')]
update:
    fwupdmgr refresh --force
    fwupdmgr update
    flatpak update -y --user
    flatpak update -y --system
    brew update
    rpm-ostree update

# Check background download of updates is enabled
[group('Maintenance')]
auto-update-status:
    #!/usr/bin/bash
    echo "Automatic updates are $(systemctl is-enabled rpm-ostreed-automatic.timer)"

# Toggle background download of updates on/off
[group('Maintenance')]
toggle-updates:
    #!/usr/bin/bash
    if [[ $(systemctl is-enabled rpm-ostreed-automatic.timer) == enabled ]]; then
        echo "Disabling automatic updates"
        systemctl disable --now rpm-ostreed-automatic.timer
    else
        echo "Enabling automatic updates"
        systemctl enable --now rpm-ostreed-automatic.timer
    fi
